plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "maven"
    id 'maven-publish'
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

version = "0.2.11.alpha.0"
group = "com.treasuredata.embulk.plugins"
description = "JIRA Embulk input plugin."

sourceCompatibility = 1.8
targetCompatibility = 1.8

def embulkVersion = '0.10.19'

dependencies {
    compileOnly "org.embulk:embulk-api:$embulkVersion"
    compileOnly "org.embulk:embulk-spi:$embulkVersion"

    // Required through GuessExecutor usage.
    compileOnly "org.embulk:embulk-core:$embulkVersion"

    compile('org.embulk:embulk-util-config:0.1.4') {
        // Conflict with Embulk Core
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }
    compile('org.embulk:embulk-util-retryhelper:0.8.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compile 'org.embulk:embulk-util-timestamp:0.2.1'

    compile('org.embulk:embulk-util-json:0.1.0') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'org.msgpack', module: 'msgpack-core'
    }

    // Explicit dependencies for embulk-util-* that matches with Embulk 0.10.19
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7'
    compile 'javax.validation:validation-api:1.1.0.Final'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.27'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile 'com.google.guava:guava:18.0'

    testCompile "junit:junit:4.+"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "org.embulk:embulk-junit4:$embulkVersion"
    testCompile "org.embulk:embulk-core:$embulkVersion:tests"
    testCompile "org.embulk:embulk-deps:$embulkVersion"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.input.jira.JiraInputPlugin"
    category = "input"
    type = "jira"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

gem {
    authors = ['uu59', 'yoshihara']
    email = ['k@uu59.org', 'h.yoshihara@everyleaf.com']
    summary = project.description
    homepage = 'https://github.com/treasure-data/embulk-input-jira'
    licenses = ['Apache-2.0']

    generateRubyCode  = false
    into("lib/embulk/input/") {
        from "lib/embulk/input/jira.rb"
    }
    into("lib/embulk/guess/") {
        from "lib/embulk/guess/jira.rb"
    }
}

gemPush {
    host = "https://rubygems.org"
}