plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "maven"
    id 'maven-publish'
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

version = "0.2.11.SNAPSHOT"
group = "com.treasuredata.embulk.plugins"
description = "JIRA Embulk input plugin."

sourceCompatibility = 1.8
targetCompatibility = 1.8

def embulkVersion = '0.9.20'

dependencies {
    compileOnly "org.embulk:embulk-core:$embulkVersion"

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.27'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    testCompile "junit:junit:4.+"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "org.embulk:embulk-test:$embulkVersion"
    testCompile "org.embulk:embulk-core:$embulkVersion:tests"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.input.jira.JiraInputPlugin"
    category = "input"
    type = "jira"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

gem {
    authors = ['uu59', 'yoshihara']
    email = ['k@uu59.org', 'h.yoshihara@everyleaf.com']
    summary = project.description
    homepage = 'https://github.com/treasure-data/embulk-input-jira'
    licenses = ['Apache-2.0']

    generateRubyCode  = false
    into("lib/embulk/input/") {
        from "lib/embulk/input/jira.rb"
    }
    into("lib/embulk/guess/") {
        from "lib/embulk/guess/jira.rb"
    }
}

gemPush {
    host = "https://rubygems.org"
}